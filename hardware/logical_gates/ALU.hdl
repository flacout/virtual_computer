// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Process x.
    Not16(in=x, out=notx2);
    Mux16(a=x, b=false, sel=zx, out=xzeros);
    Mux16(a=x, b=true, sel=nx, out=xones);
    Mux4Way16(a=x, b=notx2, c=xzeros, d=xones, sel[1]=zx, sel[0]=nx, out=xx);

    // Process y.
    Not16(in=y, out=noty2);
    Mux16(a=y, b=false, sel=zy, out=yzeros);
    Mux16(a=y, b=true, sel=ny, out=yones);
    Mux4Way16(a=y, b=noty2, c=yzeros, d=yones, sel[1]=zy, sel[0]=ny, out=yy);

    // Add / And numbers.
    // carefull sub-bus only on chip pin not on internal pin)
    Add16(a=xx, b=yy, out=outadd);
    And16(a=xx, b=yy, out=outand);
    Not16(in=outadd, out=notadd);
    Not16(in=outand, out=notand);
    Mux4Way16(a=outand, b=notand, c=outadd, d=notadd, sel[1]=f, sel[0]=no, 
              out=out, out[15]=ng, out[0..7]=leftout, out[8..15]=rightout);

    // Logic comparison ng zr.
    Or8Way(in=leftout, out=zer1);
    Or8Way(in=rightout, out=zer2);
    Or(a=zer1, b=zer2, out=zer3);
    Not(in=zer3, out=zr);
}
